prototype(Neos.NodeTypes:Page) < prototype(Neos.Neos:Page) {
    head {

        appCss = NeosRulez.ScssParser:ScssFile
        appCss {
            source = 'resource://Raw.Site/Private/Styles/app.scss'
            inline = FALSE
            format = 'compressed'
            outputFolder = 'resource://Raw.Site/Public/Styles/'
        }

        metadata = Neos.Fusion:Template {
            templatePath = 'resource://Raw.Site/Private/Templates/Page/Default.html'
            sectionName = 'metadata'
        }

        backendStylesheets = Neos.Fusion:Template {
            templatePath = 'resource://Raw.Site/Private/Templates/Page/Default.html'
            sectionName = 'backendStylesheets'
            @process.1 = ${node.context.workspaceName != 'live' ? value : ''}
        }

        titleTag = Neos.Fusion:Tag {
            tagName = 'title'
            content = ${q(node).property('title') + (nodeIterator.isLast ? '' : ' - ') + Configuration.setting('Raw.Site.pageTitle')}
        }
    }

    bodyTag.attributes.class = ${q(node).parents().count() >= 1 && q(node).children('[instanceof Neos.Neos:Document]').filter('[_hiddenInIndex=false]').count() > 0 ? 'has-subpages' : ''}

    body {
        templatePath = 'resource://Raw.Site/Private/Templates/Page/Default.html'
        sectionName = 'body'

        parts {
            menu = NeosRulez.Bootstrap:MainMenu
        }

        content {
            main = Neos.Neos:PrimaryContent {
                nodePath = 'main'
            }
        }

        footer = Neos.Neos:ContentCollection {
            nodePath = ${q(site).children('footer').property('_path')}
            collection = ${q(site).children('footer').children()}
        }

        javascripts.site = Neos.Fusion:Template {
            templatePath = 'resource://Raw.Site/Private/Templates/Page/Default.html'
            sectionName = 'bodyScripts'
        }

        @process.replaceQuotationMarks1 = ${String.replace(value, '„', '"')}
        @process.replaceQuotationMarks2 = ${String.replace(value, '“', '"')}
    }

    appJs = NeosRulez.JsMinifier:JsFile
    appJs {
        @position = 'after NeosRulezBootstrapJs'
        source = 'resource://Raw.Site/Private/JavaScript/app.js'
        inline = FALSE
        outputFolder = 'resource://Raw.Site/Public/JavaScript/'
    }
}

page = Neos.NodeTypes:Page